apply plugin: "java"

group = "org.calql"
version = "0.1.0-SNAPSHOT"
description "Queries for calendars in the world"

repositories {
    mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    testCompile "org.junit.jupiter:junit-jupiter-api:5.4.2"
    testCompile "org.junit.jupiter:junit-jupiter-params:5.4.2"

    testRuntime "org.junit.jupiter:junit-jupiter-engine:5.4.2"
}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    options.encoding = "UTF-8"
}

jar {
    from rootProject.file("LICENSE")
    from rootProject.file("NOTICE")
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource
    from rootProject.file("LICENSE")
    from rootProject.file("NOTICE")
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = "javadoc"
    from javadoc.destinationDir
    from rootProject.file("LICENSE")
    from rootProject.file("NOTICE")
}

artifacts {
    archives sourcesJar, javadocJar
}

test {
    useJUnitPlatform()
    testLogging {
        outputs.upToDateWhen { false }
        showStandardStreams = true
        events "passed", "skipped", "failed", "standardOut", "standardError"
    }
}
